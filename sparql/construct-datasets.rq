PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dgfr: <http://colin.maudry.com/ontologies/dgfr#>
PREFIX xs: <http://www.w3.org/2001/XMLSchema#>
PREFIX apf: <http://jena.hpl.hp.com/ARQ/property#>

construct

{

  ?dataset a dcat:Dataset ;
    dct:identifier ?id ;
    dct:title ?title ;
    rdfs:label ?title ;
    dcat:landingPage ?urlAsUri ;
    dct:rightsHolder ?organization ;
    dct:publisher ?supplier ;
    dct:description ?description ;
    dct:accrualPeriodicity ?dctFrequency ;
    dct:license ?license ;
    dgfr:private ?lcPrivate ;
    dgfr:featured ?lcFeatured ;
    dct:created ?createdAsDateTime ;
    dct:modified ?modifiedAsDateTime ;
    dcat:keywords ?tags ;
   	dgfr:slug ?slug ;
    dgfr:views ?views ;
    dgfr:followers ?followers ;
    dgfr:reuses ?reuses ;
    dgfr:visits ?visits ;
    dgfr:hits ?hits ;
    dgfr:uniqueVisitors ?unique_visitors ;
    dgfr:issues ?issues .  
}
from <file:../csv/datasets.csv#delimiter=%3B;encoding=utf-8>
  where {
  
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?slug)) as ?dataset)
  bind(uri(?url) as ?urlAsUri)
  bind(uri(concat("http://purl.org/cld/freq/",?frequency)) as ?dctFrequency)
  bind(xs:boolean(if (?private="",false,lcase(?private))) as ?lcPrivate)
  bind(xs:boolean(if (?featured="",false,lcase(?featured))) as ?lcFeatured)
  bind(xs:dateTime(?created_at) as ?createdAsDateTime)
  bind(xs:dateTime(?last_modified) as ?modifiedAsDateTime)
  
  bind(xs:integer(?metric_views) as ?views)
  bind(xs:integer(?metric_issues) as ?issues)
  bind(xs:integer(?metric_followers) as ?followers)
  bind(xs:integer(?metric_nb_hits) as ?hits)
  bind(xs:integer(?metric_nb_visits) as ?visits)
  bind(xs:integer(?metric_reuses) as ?reuses)
  bind(xs:integer(?metric_nb_uniq_visitors) as ?unique_visitors)

  }
  
  