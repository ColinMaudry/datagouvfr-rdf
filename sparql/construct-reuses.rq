PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dgfr: <http://colin.maudry.com/ontologies/dgfr#>
PREFIX xs: <http://www.w3.org/2001/XMLSchema#>
PREFIX apf: <http://jena.hpl.hp.com/ARQ/property#>
PREFIX schema: <http://schema.org/url>
PREFIX prov: <http://www.w3.org/ns/prov#> 
PREFIX datasets: <https://www.data.maudry.com/fr/datasets/>
PREFIX reuses: <https://www.data.maudry.com/fr/reuses/> 

construct

{

  ?reuse a dgfr:Reuse ;
    dct:identifier ?id ;
    dct:title ?title ;
    rdfs:label ?title ;
	foaf:page ?remoteUrlAsUri ;
	schema:url ?urlAsUri ;
    dct:rightsHolder ?organization ;
    dct:publisher ?supplier ;
    dct:description ?description ;
    dgfr:featured ?lcFeatured ;
    dct:created ?createdAsDateTime ;
    dct:modified ?modifiedAsDateTime ;
    dcat:keywords ?tags ;
   	dgfr:slug ?slug ;
    dgfr:views ?views ;
    dgfr:followers ?followers ;
    dgfr:datasets ?nb_datasets ;
    dgfr:visits ?visits ;
    dgfr:hits ?hits ;
    prov:used ?usedDataset1 ;
    prov:used ?usedDataset2 ;
    prov:used ?usedDataset3 ;
    prov:used ?usedDataset4 ;
    prov:used ?usedDataset5 ;
    prov:used ?usedDataset6 ;
    prov:used ?usedDataset7 ;
    prov:used ?usedDataset8 ;
    prov:used ?usedDataset9 ;
    prov:used ?usedDataset10 ;
    dgfr:uniqueVisitors ?unique_visitors .  
}
from <file:../csv/reuses.csv#delimiter=%3B;encoding=utf-8>
  where {
  bind(uri(concat("https://www.data.maudry.com/fr/reuses/",?id)) as ?reuse)
  bind(uri(?url) as ?urlAsUri)
  bind(uri(?remote_url) as ?remoteUrlAsUri)
  bind(xs:boolean(if (?featured="",false,lcase(?featured))) as ?lcFeatured)
  bind(xs:dateTime(?created_at) as ?createdAsDateTime)
  bind(xs:dateTime(?last_modified) as ?modifiedAsDateTime)
  bind(xs:integer(?metric_views) as ?views)
  bind(xs:integer(?metric_followers) as ?followers)
  bind(xs:integer(?metric_nb_hits) as ?hits)
  bind(xs:integer(?metric_nb_visits) as ?visits)
  bind(xs:integer(?metric_datasets) as ?nb_datasets)
  bind(xs:integer(?metric_nb_uniq_visitors) as ?unique_visitors)
  
  #The ugly part: max 10 source datasets are supported per reuse... We'll see how it goes and expand to more if needed.
  #Thanks ThomasFrancart
  #http://answers.semanticweb.com/answer_link/19218/  
  
  bind(if(contains(?datasets, ","),strbefore(?datasets, ","), ?datasets) as ?usedDatasetId1)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId1)) as ?usedDataset1)
  bind(strafter(?datasets, ",") as ?r1)
  bind(if(?r1 = "",?dummy,if(contains(?r1, ","),strbefore(?r1, ","), ?r1)) as ?usedDatasetId2)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId2)) as ?usedDataset2)
  bind(strafter(?r1, ",") as ?r2)
  bind(if(?r2 = "",?dummy,if(contains(?r2, ","),strbefore(?r2, ","), ?r2)) as ?usedDatasetId3)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId3)) as ?usedDataset3)
  bind(strafter(?r2, ",") as ?r3)
  bind(if(?r3 = "",?dummy,if(contains(?r3, ","),strbefore(?r3, ","), ?r3)) as ?usedDatasetId4)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId4)) as ?usedDataset4)
  bind(strafter(?r3, ",") as ?r4)
  bind(if(?r4 = "",?dummy,if(contains(?r4, ","),strbefore(?r4, ","), ?r4)) as ?usedDatasetId5)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId5)) as ?usedDataset5)
  bind(strafter(?r4, ",") as ?r5)
  bind(if(?r5 = "",?dummy,if(contains(?r5, ","),strbefore(?r5, ","), ?r5)) as ?usedDatasetId6)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId6)) as ?usedDataset6)
  bind(strafter(?r5, ",") as ?r6)
  bind(if(?r6 = "",?dummy,if(contains(?r6, ","),strbefore(?r6, ","), ?r6)) as ?usedDatasetId7)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId7)) as ?usedDataset7)
  bind(strafter(?r6, ",") as ?r7)
  bind(if(?r7 = "",?dummy,if(contains(?r7, ","),strbefore(?r7, ","), ?r7)) as ?usedDatasetId8)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId8)) as ?usedDataset8)
  bind(strafter(?r7, ",") as ?r8)
  bind(if(?r8 = "",?dummy,if(contains(?r8, ","),strbefore(?r8, ","), ?r8)) as ?usedDatasetId9)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId9)) as ?usedDataset9)
  bind(strafter(?r8, ",") as ?r9)
  bind(if(?r9 = "",?dummy,if(contains(?r9, ","),strbefore(?r9, ","), ?r9)) as ?usedDatasetId10)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?usedDatasetId10)) as ?usedDataset10)
  }
  
  