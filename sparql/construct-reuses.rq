PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dgfr: <http://colin.maudry.com/ontologies/dgfr#>
PREFIX xs: <http://www.w3.org/2001/XMLSchema#>
PREFIX apf: <http://jena.hpl.hp.com/ARQ/property#>
PREFIX schema: <http://schema.org/url>
PREFIX prov: <http://www.w3.org/ns/prov#> 

construct

{

  ?reuse a dgfr:Reuse ;
    dct:identifier ?id ;
    dct:title ?title ;
    rdfs:label ?title ;
	foaf:page ?remoteUrlAsUri ;
	schema:url ?urlAsUri ;
    dct:rightsHolder ?organization ;
    dct:publisher ?supplier ;
    dct:description ?description ;
    dgfr:featured ?lcFeatured ;
    dct:created ?createdAsDateTime ;
    dct:modified ?modifiedAsDateTime ;
    dcat:keywords ?tags ;
   	dgfr:slug ?slug ;
    dgfr:views ?views ;
    dgfr:followers ?followers ;
    dgfr:datasets ?nb_datasets ;
    dgfr:visits ?visits ;
    dgfr:hits ?hits ;
    prov:used ?usedDataset1 ;
    prov:used ?usedDataset2 ;
    prov:used ?usedDataset3 ;
    dgfr:uniqueVisitors ?unique_visitors .  
}
from <file:../csv/reuses.csv#delimiter=%3B;encoding=utf-8>
  where {
  bind(uri(concat("https://www.data.maudry.com/fr/reuses/",?id)) as ?reuse)
  bind(uri(?url) as ?urlAsUri)
  bind(uri(?remote_url) as ?remoteUrlAsUri)
  bind(xs:boolean(if (?featured="",false,lcase(?featured))) as ?lcFeatured)
  bind(xs:dateTime(?created_at) as ?createdAsDateTime)
  bind(xs:dateTime(?last_modified) as ?modifiedAsDateTime)
  bind(xs:integer(?metric_views) as ?views)
  bind(xs:integer(?metric_followers) as ?followers)
  bind(xs:integer(?metric_nb_hits) as ?hits)
  bind(xs:integer(?metric_nb_visits) as ?visits)
  bind(xs:integer(?metric_datasets) as ?nb_datasets)
  bind(xs:integer(?metric_nb_uniq_visitors) as ?unique_visitors)
  
  #The ugly part
  bind(if(contains(?datasets,","),strbefore(?datasets,","),?datasets) as ?datasetId1)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?datasetId1)) as ?usedDataset1)
  bind(strbefore(strafter(?datasets,concat(?datasetId1,",")),",") as ?datasetId2)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?datasetId2)) as ?usedDataset2)
  bind(strbefore(strafter(?datasets,concat(?datasetId2,",")),",") as ?datasetId3)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?datasetId3)) as ?usedDataset3)
  }
  
  