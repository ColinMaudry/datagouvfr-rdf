PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dgfr: <http://colin.maudry.com/ontologies/dgfr#>
PREFIX xs: <http://www.w3.org/2001/XMLSchema#>
PREFIX apf: <http://jena.hpl.hp.com/ARQ/property#>

construct

{

  ?resource a dcat:Distribution ;
  	dgfr:dataset ?dataset ;
    dct:identifier ?id ;
    dcat:downloadUrl ?urlAsUri ;
    dct:title ?title ;
    rdfs:label ?title ;
    dct:description ?description ;
    dgfr:type ?lcType ;
    dgfr:format ?lcFormat ;
    dcat:mediaType ?mime ;
    dcat:byteSize ?sizeAsInt ;
    dgfr:checksumType ?checksum_type ;
    dgfr:checksumValue ?checksum_value ;
    dct:issued ?createdAsDateTime ;
    dct:modified ?modifiedAsDateTime ;
    dgfr:downloads ?downloadsAsInt .
      
  ?dataset dcat:distribution ?resource .
}
from <file:../csv/resources.csv#delimiter=%3B;encoding=utf-8>
  where {
  
  bind(uri(concat("https://www.data.maudry.com/fr/resources/",?id)) as ?resource)
  bind(uri(concat("https://www.data.maudry.com/fr/datasets/",?dataset_slug)) as ?dataset)
  bind(if(contains(?url," "), uri(encode_for_uri(?url)),uri(?url)) as ?urlAsUri)
  bind(xs:dateTime(?created_at) as ?createdAsDateTime)
  bind(xs:dateTime(?modified) as ?modifiedAsDateTime)
  bind(xs:integer(?size) as ?sizeAsInt)
  bind(xs:integer(?downloads) as ?downloadsAsInt)
  bind(lcase(?format) as ?lcFormat)
  bind(lcase(?type) as ?lcType)
  }
  
  